--$START  c:\Users\User\Desktop\Triple-T\main.src
program TicTacToe;

--$START  c:\Users\User\Desktop\Triple-T\config.iri 
 -------------------------------------------------
-- Used with iRev Configuration File
-- C:\Users\User\Desktop\Triple-T\main.920
-------------------------------------------------


-- Scale Aliases

-- Setpoint Aliases

-- Widget Aliases
Bar1S1   : constant integer := 1;
Graph3   : constant integer := 2;
Bar3S1   : constant integer := 3;
Graph5   : constant integer := 4;
Graph6   : constant integer := 5;
Sym1S1   : constant integer := 6;
Sym2S1   : constant integer := 7;
Sym3S1   : constant integer := 8;
Sym4S1   : constant integer := 9;
Sym5S1   : constant integer := 10;
Sym6S1   : constant integer := 11;
Sym7S1   : constant integer := 12;
Sym8S1   : constant integer := 13;
Sym9S1   : constant integer := 14;
Sym10S1  : constant integer := 15;
Sym11S1  : constant integer := 16;
Sym12S1  : constant integer := 17;
Sym13S1  : constant integer := 18;
Sym14S1  : constant integer := 19;
Sym15S1  : constant integer := 20;
Sym16S1  : constant integer := 21;
Sym17S1  : constant integer := 22;
Sym18S1  : constant integer := 23;
Sym19S1  : constant integer := 24;
Sym20S1  : constant integer := 25;
Sym21S1  : constant integer := 26;
Sym22S1  : constant integer := 27;
Sym23S1  : constant integer := 28;
Sym24S1  : constant integer := 29;
Lbl1S1   : constant integer := 30;
Lbl2S1   : constant integer := 31;
Lbl3S1   : constant integer := 32;
Lbl4S1   : constant integer := 33;

-- Serial Port Aliases
Port1    : constant integer := 1;
Port2    : constant integer := 2;
Port3    : constant integer := 3;
Port4    : constant integer := 4;

-- Analog Output Card Aliases

-- Digital I/O Aliases
DIOCRD0  : constant integer := 0;
DIO0_1   : constant integer := 1;
DIO0_2   : constant integer := 2;
DIO0_3   : constant integer := 3;
DIO0_4   : constant integer := 4;
DIO0_5   : constant integer := 5;
DIO0_6   : constant integer := 6;
DIO0_7   : constant integer := 7;
DIO0_8   : constant integer := 8;
DIO0_9   : constant integer := 9;
DIO0_10  : constant integer := 10;
DIO0_11  : constant integer := 11;
DIO0_12  : constant integer := 12;
DIO0_13  : constant integer := 13;
DIO0_14  : constant integer := 14;
DIO0_15  : constant integer := 15;
DIO0_16  : constant integer := 16;
DIO0_17  : constant integer := 17;
DIO0_18  : constant integer := 18;
DIO0_19  : constant integer := 19;
DIO0_20  : constant integer := 20;
DIO0_21  : constant integer := 21;
DIO0_22  : constant integer := 22;
DIO0_23  : constant integer := 23;
DIO0_24  : constant integer := 24;

-- Database Type Definitions and Field Aliases
type TruckDatabase is Database ("Truck")
	ID       : string;
	Pri_Wgt  : real;
	Sec_Wgt  : real;
	Ter_Wgt  : real;
	TimeDate : datetime;
	Scale    : integer;
	Keyed    : integer;
end database;

Truck_ID          : constant integer := 1;
Truck_Pri_Wgt     : constant integer := 2;
Truck_Sec_Wgt     : constant integer := 3;
Truck_Ter_Wgt     : constant integer := 4;
Truck_TimeDate    : constant integer := 5;
Truck_Scale       : constant integer := 6;
Truck_Keyed       : constant integer := 7;



 
 --$END  c:\Users\User\Desktop\Triple-T\config.iri
----------------------------------------------------------------------------
--$START  c:\Users\User\Desktop\Triple-T\generals\constants.iri 
 ----------------------------------------------------------------------------
TRUE: constant integer := 1;
FALSE: constant integer := 0;
----------------------------------------------------------------------------
TOP_ARROW_INCREMENT: constant integer := 1;
LEFT_ARROW_INCREMENT: constant integer := 3;
----------------------------------------------------------------------------
MAX_TOP_ARROW_POSITION: constant integer := 2;
MAX_LEFT_ARROW_POSITION: constant integer := 6;
----------------------------------------------------------------------------
STARTING_CIRCLE_ID: constant integer := 6;
STARTING_SQUARE_ID: constant integer := 15;
----------------------------------------------------------------------------
STARTING_TOP_ARRROW_ID: constant integer := 24;
STARTING_LEFT_ARRROW_ID: constant integer := 27;
----------------------------------------------------------------------------
DEFAULT_STATE: constant integer := 1;
FILL_STATE: constant integer := 2;
EMPTY_STATE: constant integer := 3;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\generals\constants.iri
--$START  c:\Users\User\Desktop\Triple-T\generals\variables.iri 
 ----------------------------------------------------------------------------
g_Player1Score: integer := 0;
g_Player2Score: integer := 0;
----------------------------------------------------------------------------
g_TopArrowPosition: integer := 0;
g_LeftArrowPosition: integer := 0;
----------------------------------------------------------------------------
g_SelectedSymbol: integer := 0;
g_CurrentPlayer: integer := 1;
----------------------------------------------------------------------------
type arrIntegers is array[9] of integer;
----------------------------------------------------------------------------
g_arrPlayer1Symbols: arrIntegers;
g_arrPlayer2Symbols: arrIntegers;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\generals\variables.iri
----------------------------------------------------------------------------
--$START  c:\Users\User\Desktop\Triple-T\util\symbol_manager.iri 
 ----------------------------------------------------------------------------
function GetSelectedSymbol: integer;
begin
    -- DisplayStatus("Pos: " + IntegerToString(g_TopArrowPosition + g_LeftArrowPosition + 1, 0));
    -- ProgramDelay(150);
    return g_TopArrowPosition + g_LeftArrowPosition + 1;
end;
----------------------------------------------------------------------------
function IsSymbolPlayable(symbolId: integer): integer;
begin
    if (g_arrPlayer1Symbols[symbolId] = TRUE
        or g_arrPlayer2Symbols[symbolId] = TRUE) then
        -- DisplayStatus("Sym " + IntegerToString(symbolId, 0) + " is not playable");
        -- ProgramDelay(150);
        return FALSE;
    end if;
    -- DisplayStatus("Sym " + IntegerToString(symbolId, 0) + " is playable");
    -- ProgramDelay(150);
    return TRUE;
end;
----------------------------------------------------------------------------
procedure StoreSelectedSymbol(playerNumber: integer);
begin
    if (playerNumber = 1) then
        g_arrPlayer1Symbols[g_SelectedSymbol] := TRUE;
        -- DisplayStatus("Sym " + IntegerToString(g_SelectedSymbol, 0) + " played by player 1");
        -- ProgramDelay(150);
    elsif (playerNumber = 2) then
        g_arrPlayer2Symbols[g_SelectedSymbol] := TRUE;
        -- DisplayStatus("Sym " + IntegerToString(g_SelectedSymbol, 0) + " played by player 2");
        -- ProgramDelay(150);
    end if;
end;
----------------------------------------------------------------------------
procedure DrawSelectedSymbol(playerNumber: integer);
    symbolId: integer;
begin
    if (playerNumber = 1) then
        symbolId := STARTING_CIRCLE_ID + g_SelectedSymbol - 1;
    elsif (playerNumber = 2) then
        symbolId := STARTING_SQUARE_ID + g_SelectedSymbol - 1;
    end if;

    SetSymbolState(symbolId, DEFAULT_STATE);
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\util\symbol_manager.iri
--$START  c:\Users\User\Desktop\Triple-T\util\array_manager.iri 
 ----------------------------------------------------------------------------
function ArrayMatch(arr1: arrIntegers; arr2: arrIntegers): integer;
    i: integer;
begin
    for i := 1 to 9
    loop
        if (arr1[i] <> arr2[i]) then
            return FALSE;
        end if;
    end loop;
    return TRUE;
end;
----------------------------------------------------------------------------
function NewArray: arrIntegers;
    arr: arrIntegers;
    i: integer;
begin
    for i := 1 to 9
    loop
        arr[i] := FALSE;
    end loop;

    return arr;
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\util\array_manager.iri
--$START  c:\Users\User\Desktop\Triple-T\util\arrow_manager.iri 
 ----------------------------------------------------------------------------
function SetArrowPosition(
    arrowPosition: integer; 
    incrementValue: integer; 
    maxValue: integer): integer;
    newPosition: integer;
begin   
    if (arrowPosition = maxValue 
        and incrementValue > 0) then
        return 0;
    end if;
    
    newPosition := arrowPosition + incrementValue;

    if (newPosition < 0) then
        return maxValue;
    end if;

    return newPosition;
end;
----------------------------------------------------------------------------
procedure DrawArrow(arrrowPosition: integer; startingId: integer; incrementValue: integer);
    i: integer;
    arrowState: integer;
    state: integer;
begin
    if (arrrowPosition = (incrementValue * 0)) then
        arrowState := 1;
    elsif (arrrowPosition = (incrementValue * 1)) then
        arrowState := 2;
    elsif (arrrowPosition = (incrementValue * 2)) then
        arrowState := 3;
    end if;

    for i := 1 to 3
    loop
        if (i = arrowState) then
            state := DEFAULT_STATE;
        else
            state := EMPTY_STATE;
        end if;

        SetSymbolState(startingId + (i - 1), state);
    end loop;
end;
----------------------------------------------------------------------------
procedure RestartArrowsPosition;
begin
    g_TopArrowPosition := 0;
    g_LeftArrowPosition := 0;
    DrawArrow(g_TopArrowPosition, STARTING_TOP_ARRROW_ID, TOP_ARROW_INCREMENT);
    DrawArrow(g_LeftArrowPosition, STARTING_LEFT_ARRROW_ID, LEFT_ARROW_INCREMENT);
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\util\arrow_manager.iri
--$START  c:\Users\User\Desktop\Triple-T\util\clear_screen.iri 
 ----------------------------------------------------------------------------
procedure DrawSymbols(startingId: integer; state: integer; totalWidgets: integer);
    i: integer;
    lastId: integer;
begin
    lastId := startingId + totalWidgets;
    for i := startingId to lastId
    loop
        SetWidgetVisibility(i, Von);
        SetSymbolState(i, state);
    end loop;
end;
----------------------------------------------------------------------------
procedure ClearScreen;
begin
    DrawSymbols(STARTING_CIRCLE_ID, EMPTY_STATE, 25);
end;
----------------------------------------------------------------------------
procedure RestartGame;
begin
    SetLabelText(Lbl3S1, IntegerToString(g_Player1Score, 0));
    SetLabelText(Lbl4S1, IntegerToString(g_Player2Score, 0));
    g_arrPlayer1Symbols := NewArray;
    g_arrPlayer2Symbols := NewArray;
    RestartArrowsPosition;
    ClearScreen;
    g_CurrentPlayer := 1;
    DisplayStatus("Player 1 Turn!");
    SetSymbolState(STARTING_LEFT_ARRROW_ID, DEFAULT_STATE);
    SetSymbolState(STARTING_TOP_ARRROW_ID, DEFAULT_STATE);
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\util\clear_screen.iri
--$START  c:\Users\User\Desktop\Triple-T\util\go_next_turn.iri 
 ----------------------------------------------------------------------------
procedure GoToNextTurn;
begin
    if (g_CurrentPlayer = 1) then
        g_CurrentPlayer := 2;
        DisplayStatus("Player 2 Turn!");
    elsif (g_CurrentPlayer = 2) then
        g_CurrentPlayer := 1;
        DisplayStatus("Player 1 Turn!");
    end if;
    RestartArrowsPosition;
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\util\go_next_turn.iri
--$START  c:\Users\User\Desktop\Triple-T\util\check_win.iri 
 ----------------------------------------------------------------------------
function IsWinnerPlayHelper(arrSymbols: arrIntegers; n1: integer; n2: integer; n3: integer): integer;
    winnerArray: arrIntegers;
    strippedArray: arrIntegers;
    i: integer;
begin
    strippedArray := NewArray;
    winnerArray := NewArray;

    strippedArray[n1] := arrSymbols[n1];
    strippedArray[n2] := arrSymbols[n2];
    strippedArray[n3] := arrSymbols[n3];

    winnerArray[n1] := TRUE;
    winnerArray[n2] := TRUE;
    winnerArray[n3] := TRUE;

    return ArrayMatch(strippedArray, winnerArray);
end;
----------------------------------------------------------------------------
function IsWinnerPlay(arrSymbols: arrIntegers): integer;
begin
    if (IsWinnerPlayHelper(arrSymbols, 1, 2, 3) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 4, 5, 6) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 7, 8, 9) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 1, 4, 7) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 2, 5, 8) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 3, 6, 9) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 1, 5, 9) = TRUE) then  
        return TRUE;
    elsif (IsWinnerPlayHelper(arrSymbols, 3, 5, 7) = TRUE) then  
        return TRUE;
    end if;

    return FALSE;
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\util\check_win.iri
--$START  c:\Users\User\Desktop\Triple-T\util\win_game.iri 
 ----------------------------------------------------------------------------
procedure DrawWinnerPlay(playerNumber: integer);
    i: integer;
    symbolId: integer;
    winnerArray: arrIntegers;
begin
    if (playerNumber = 1) then
        symbolId := STARTING_CIRCLE_ID;
        winnerArray := g_arrPlayer1Symbols;
    elsif (playerNumber = 2) then
        symbolId := STARTING_SQUARE_ID;
        winnerArray := g_arrPlayer2Symbols;
    end if;

    for i := 1 to 9
    loop
        if (winnerArray[i] <> 0) then
            SetSymbolState(symbolId + i - 1, FILL_STATE);
        end if;
    end loop;
end;
----------------------------------------------------------------------------
procedure SetRoundWinner(playerNumber: integer);
begin
    if (playerNumber = 1) then
        g_Player1Score := g_Player1Score + 1;
        DisplayStatus("Player 1 Won!");
    elsif (playerNumber = 2) then
        g_Player2Score := g_Player2Score + 1;
        DisplayStatus("Player 2 Won!");
    end if;
    SetLabelText(Lbl3S1, IntegerToString(g_Player1Score, 0));
    SetLabelText(Lbl4S1, IntegerToString(g_Player2Score, 0));
end;
----------------------------------------------------------------------------
procedure CheckForWin;
    winnerPlayer: integer;
begin
    winnerPlayer := 0;
    if (IsWinnerPlay(g_arrPlayer1Symbols) = TRUE) then
        winnerPlayer := 1;
    elsif (IsWinnerPlay(g_arrPlayer2Symbols) = TRUE) then
        winnerPlayer := 2;
    end if;

    if (winnerPlayer = 0) then
        return;
    end if;

    DrawWinnerPlay(winnerPlayer);
    SetRoundWinner(winnerPlayer);
    ProgramDelay(150);
    RestartGame;
end;
----------------------------------------------------------------------------
 
 --$END  c:\Users\User\Desktop\Triple-T\util\win_game.iri
----------------------------------------------------------------------------
--$START  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_down.iri 
 handler NavDownKeyPressed;
begin
    g_LeftArrowPosition := SetArrowPosition(g_LeftArrowPosition,
        LEFT_ARROW_INCREMENT,
        MAX_LEFT_ARROW_POSITION);
    DrawArrow(g_LeftArrowPosition, STARTING_LEFT_ARRROW_ID, LEFT_ARROW_INCREMENT);
end; 
 --$END  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_down.iri
--$START  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_up.iri 
 handler NavUpKeyPressed;
begin
    g_LeftArrowPosition := SetArrowPosition(g_LeftArrowPosition,
        (LEFT_ARROW_INCREMENT * -1),
        MAX_LEFT_ARROW_POSITION);
    DrawArrow(g_LeftArrowPosition, STARTING_LEFT_ARRROW_ID, LEFT_ARROW_INCREMENT);
end; 
 --$END  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_up.iri
--$START  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_left.iri 
 handler NavLeftKeyPressed;
begin
    g_TopArrowPosition := SetArrowPosition(g_TopArrowPosition, 
        (TOP_ARROW_INCREMENT * -1), 
        MAX_TOP_ARROW_POSITION);
    DrawArrow(g_TopArrowPosition, STARTING_TOP_ARRROW_ID, TOP_ARROW_INCREMENT);
end; 
 --$END  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_left.iri
--$START  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_right.iri 
 handler NavRightKeyPressed;
begin
    g_TopArrowPosition := SetArrowPosition(g_TopArrowPosition, 
        TOP_ARROW_INCREMENT, 
        MAX_TOP_ARROW_POSITION);
    DrawArrow(g_TopArrowPosition, STARTING_TOP_ARRROW_ID, TOP_ARROW_INCREMENT);
end; 
 --$END  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\nav_right.iri
--$START  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\enter.iri 
 ----------------------------------------------------------------------------
handler EnterKeyPressed;
begin
    g_SelectedSymbol := GetSelectedSymbol;

    if (IsSymbolPlayable(g_SelectedSymbol) = TRUE) then
        StoreSelectedSymbol(g_CurrentPlayer);
        DrawSelectedSymbol(g_CurrentPlayer);
        GoToNextTurn;
    end if;

    CheckForWin;
end;
---------------------------------------------------------------------------- 
 --$END  c:\Users\User\Desktop\Triple-T\handlers\key_pressed\enter.iri
----------------------------------------------------------------------------
begin
    RestartGame;
end TicTacToe;
--$END  c:\Users\User\Desktop\Triple-T\main.src
